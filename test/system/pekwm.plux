[doc]
Run system tests using Xvfb

Start a single Xvfb and pekwm session and run all pekwm system tests
in one go.
[enddoc]

[global BIN_DIR=../../build/src]
[global TEST_DIR=../../build/test/system]
[global DISPLAY=:1]

# test possible ways of setting the configuration file used by pekwm
[function test_config_path]
    [log test_config_path]
    !$BIN_DIR/pekwm --display $DISPLAY --config ./pekwm.config.missing --log-level trace
    ?using configuration at ./pekwm.config.missing
    ?failed to open file ./pekwm.config.missing
    !$_CTRL_C_
    ?SH-PROMPT:

    !env PEKWM_CONFIG_FILE=./pekwm.env.config.missing $BIN_DIR/pekwm --display $DISPLAY --log-level trace
    ?using configuration at ./pekwm.env.config.missing
    ?failed to open file ./pekwm.env.config.missing
    !$_CTRL_C_
    ?SH-PROMPT:

    # test without home set, supposed to fail
    !env -i $BIN_DIR/pekwm --display $DISPLAY --log-level trace
    ???failed to get configuration file path, $HOME not set.
    ?SH-PROMPT:
[endfunction]

[function test_pekwm_replace]
    [log test pekwm --replace]

    [shell pekwm-1]
        [log starting pekwm to be replaced]
        !$BIN_DIR/pekwm --display $DISPLAY --log-level trace
        ?Enter event loop.

    [shell pekwm-2]
        [log starting pekwm without --replace, should fail]
        !$BIN_DIR/pekwm --display $DISPLAY --log-level trace
        ?window manager already running
        ?SH-PROMPT:

        [log starting pekwm with --replace, replace first]
        !$BIN_DIR/pekwm --display $DISPLAY --replace --log-level trace
        ?waiting for previous window manager to exit
        ?Enter event loop.

    [shell pekwm-1]
        ?being replaced by another WM
        ?SH-PROMPT:

    [shell pekwm-2]
        !$_CTRL_C_
        ?SH-PROMPT:
[endfunction]

[function test_update_client_list_report_all]
    [log test_update_client_list_report_all]

    [shell test_update_client_list]
        ?SH-PROMPT:
        !env DISPLAY=$DISPLAY $TEST_DIR/test_update_client_list
        ?BEGIN WINDOWS
        ?END WINDOWS
        ?PROGRESS: wait for PropertyNotify

    [shell test_client-1]
        ?SH-PROMPT:
        !env DISPLAY=$DISPLAY $TEST_DIR/test_client
        ?Window ([0-9]+)
        [global window1=$1]
        [log started test client 1 $window1]

    [shell test_update_client_list]
        ?BEGIN WINDOWS
        ?Window $window1
        ?END WINDOWS
        -Window $window1

    [shell test_client-1]
        !$_CTRL_C_
        ?SH-PROMPT:
        [log stopped test client 1 $window1]

[shell test_update_client_list]
        ?BEGIN WINDOWS
        ?END WINDOWS
        -

    [shell test_client-1]
        !env DISPLAY=$DISPLAY $TEST_DIR/test_client
        ?Window ([0-9]+)
        [global window1=$1]
        [log re-started test client 1 $window1]

    [shell test_update_client_list]
        ?BEGIN WINDOWS
        ?Window $window1
        ?END WINDOWS

    [shell test_client-2]
        ?SH-PROMPT:
        !env DISPLAY=$DISPLAY $TEST_DIR/test_client
        ?Window ([0-9]+)
        [global window2=$1]
        [log started test client 2 $window2]

    [shell test_update_client_list]
        ?BEGIN WINDOWS
        ?Window $window1
        ?Window $window2
        ?END WINDOWS
        !$_CTRL_C_
        ?SH-PROMPT:
[endfunction]

[function test_update_client_list]
    [log test_update_client_list]

    [shell test_update_client_list]
        -Window $window1
        !env DISPLAY=$DISPLAY $TEST_DIR/test_update_client_list
        ?BEGIN WINDOWS
        ?Window $window2
        ?END WINDOWS
        !$_CTRL_C_
        ?SH-PROMPT:
[endfunction]

[function test_warp_pointer]
    [log test_warp_pointer]
    !$BIN_DIR/pekwm_ctrl WarpPointer 100 100
    ?SH-PROMPT:
    !$TEST_DIR/test_client query_pointer
    !root x 100 y 100
    ?SH-PROMPT:
    !$BIN_DIR/pekwm_ctrl WarpPointer 300 300
    ?SH-PROMPT:
    !$TEST_DIR/test_client query_pointer
    !root x 300 y 300
    ?SH-PROMPT:
[endfunction]

[shell Xvfb]
    ?SH-PROMPT:
    [log starting Xvfb]
    -Fatal server error
    !Xvfb -displayfd 1 $DISPLAY
    ?1

[shell pekwm]
    [log starting pekwm (pekwm.config.report_all)]
    !$BIN_DIR/pekwm --display $DISPLAY --config pekwm.config.report_all --log-level trace
    ?Enter event loop.

[shell test]
    [call test_warp_pointer]
    [call test_update_client_list_report_all]

[shell pekwm]
    !$_CTRL_C_
    ?SH-PROMPT:

    [log starting pekwm (pekwm.config)]
    !$BIN_DIR/pekwm --display $DISPLAY --config pekwm.config --log-level trace
    ?Enter event loop.

[shell test]
    [call test_update_client_list]

[shell pekwm]
    !$_CTRL_C_
    ?SH-PROMPT:

[shell test]
    [call test_config_path]
    [call test_pekwm_replace]


[shell Xvfb]
    !$_CTRL_C_
    ?SH-PROMPT:
